# Maintainer: KITAGAWA Masahiro <arctica0316@gmail.com>
pkgname="lightzone"
pkgver=4.1.1
pkgrel=0
pkgdesc="Open-source professional-level digital darkroom software"
url="http://lightzoneproject.org/"
license=('custom:BSD-3-Clause')
arch=('i686' 'x86_64')

depends=('jre7-openjdk' 'javahelp2' 'lcms2' 'libjpeg-turbo' 'libtiff')
makedepends=('jdk7-openjdk' 'apache-ant' 'autoconf' 'gcc' 'make' 'tidyhtml' 'git' 'libx11' 'pkg-config' 'rsync' 'javahelp2' 'lcms2' 'libjpeg-turbo' 'libtiff')

source=("${pkgname}-${pkgver}.tar.bz2")
md5sums=()

build() {
  # Set $JAVA_HOME and $ANT_HOME
  export JAVA_HOME=/usr/lib/jvm/java-7-openjdk
  [ -f /etc/profile.d/apache-ant.sh ] && source /etc/profile.d/apache-ant.sh

  cd "${srcdir}/${pkgname}-${pkgver}/"
  ant -f linux/build.xml jar
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  _libexecdir=/usr/lib
  install -dm 0755 "${pkgdir}/${_libexecdir}/${pkgname}"
  cp -pH linux/products/*.so "${pkgdir}/${_libexecdir}/${pkgname}"
  _javadir=/usr/share/java
  install -dm 0755 "${pkgdir}/${_javadir}/${pkgname}"
  cp -pH lightcrafts/products/dcraw_lz "${pkgdir}/${_javadir}/${pkgname}"
  cp -pH lightcrafts/products/LightZone-forkd "${pkgdir}/${_javadir}/${pkgname}"
  cp -pH linux/products/*.jar "${pkgdir}/${_javadir}/${pkgname}"
  
# create icons and shortcuts
  _datadir=/usr/share
  install -dm 0755 "${pkgdir}/${_datadir}/applications"
  install -m 644 linux/products/lightzone.desktop "${pkgdir}/${_datadir}/applications/"
  cp -pHR linux/icons "${pkgdir}/${_datadir}/"

  _bindir=/usr/bin
  install -dm 0755 "${pkgdir}/${_bindir}"
  install -m 755 "linux/products/${pkgname}" "${pkgdir}/${_bindir}"
}
